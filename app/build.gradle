import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id "com.android.application"
    id "kotlin-android"
    id "org.jetbrains.kotlin.plugin.compose"
    id "org.jetbrains.kotlin.plugin.serialization"
}

android {
    namespace = "com.androiddevchallenge.week3.americas"

    compileSdkVersion 36

    defaultConfig {
        applicationId "com.androiddevchallenge.week3.americas"
        minSdkVersion 24
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        // We use a bundled debug keystore, to allow debug builds from CI to be upgradable
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_1_8
        }
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        buildConfig false
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.8.3"
    }

    packagingOptions {
        // Multiple dependency bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {
    implementation "androidx.activity:activity-compose:1.10.1"
    implementation "androidx.compose.ui:ui:1.8.3"
    implementation "androidx.compose.material:material:1.8.3"
    implementation "androidx.compose.ui:ui-tooling:1.8.3"
    implementation "androidx.navigation:navigation-compose:2.9.3"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0"
}